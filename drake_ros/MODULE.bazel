module(name = "drake_ros")

bazel_dep(name = "rules_cc", version = "0.2.14")

DRAKE_COMMIT = "v1.48.0"

DRAKE_CHECKSUM = (
    "f03fcfebf66902a4f160605215a72d1889d33efbadc5fb008535f42ad8fd6690"
)

bazel_dep(name = "googletest", version = "1.17.0.bcr.2")

single_version_override(
    module_name = "googletest",
    patches = [
        "//tools/workspace/googletest:patches/add_drake_alias.patch",
        "//tools/workspace/googletest:patches/add_drake_printers.patch",
    ],
    # We must pin the same version as ~10 lines prior, because our patch to
    # MODULE.bazel doesn't apply correctly to other versions of googletest,
    # but Bazel's MVS selection will try to apply it anyway to all versions
    # being considered when resolving the dependency graph.
    version = "1.17.0.bcr.1",
)

bazel_dep(name = "drake")
archive_override(
    module_name = "drake",
    sha256 = DRAKE_CHECKSUM,
    strip_prefix = "drake-{}".format(DRAKE_COMMIT.lstrip("v")),
    urls = [x.format(DRAKE_COMMIT) for x in [
        "https://github.com/RobotLocomotion/drake/archive/{}.tar.gz",
    ]],
    patches = [
        "tools/workspace/drake/patches/recover_dev_dependencies.patch",
        "tools/workspace/drake/patches/use_label_in_python_lint.patch",
        "tools/workspace/drake/patches/use_label_in_cpplint.patch",
        "tools/workspace/drake/patches/use_drake_clang_format.patch",
        "tools/workspace/drake/patches/fix_gtest_printers_visibility.patch",
    ],
)

drake_dep_repositories = use_extension(
    "@drake//tools/workspace:default.bzl",
    "drake_dep_repositories",
)
use_repo(
    drake_dep_repositories,
    "eigen",
    "gtest",
    "pycodestyle",
    "python",
    "styleguide",
)

bazel_dep(name = "bazel_ros2_rules")
local_path_override(
    module_name = "bazel_ros2_rules",
    path = "../bazel_ros2_rules",
)

DRAKE_ROS_REQUIRED_PACKAGES = [
    "geometry_msgs",
    "rclpy",
    "rclcpp",
    "rosidl_runtime_c",
    "rosidl_typesupport_cpp",
    "tf2_eigen",
    "tf2_ros",
    "visualization_msgs",
    "rmw_cyclonedds_cpp",
]

DRAKE_ROS_TEST_DEPENDENCIES = [
    "test_msgs",
    "tf2_ros_py",
]

local_ros2 = use_extension(
    "@bazel_ros2_rules//lib:extensions.bzl",
    "local_ros2",
)
local_ros2.distribution(
    include_packages = (
        DRAKE_ROS_REQUIRED_PACKAGES +
        DRAKE_ROS_TEST_DEPENDENCIES
    ),
)
use_repo(local_ros2, ros2 = "local_ros2")

register_toolchains("@python//:all")
