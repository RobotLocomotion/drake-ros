# Copyright 2022 Open Source Robotics Foundation, Inc.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

load("@ros2//:ros_cc.bzl", "ros_cc_test")
load("@ros2//:ros_py.bzl", "ros_py_test")
load(
    "@drake//tools/skylark:pybind.bzl",
    "pybind_py_library",
)

PYBIND_EXCLUDES = [
    "**/*_py.cc",
    "**/*_pybind.h",
]

# TODO(sloretz) more granular targets for static linking
cc_library(
    name = "tf2",
    srcs = glob(
        [
            "*.cc",
            "*.h",
        ],
        exclude = PYBIND_EXCLUDES,
    ),
    hdrs = glob(
        ["*.h"],
        exclude = PYBIND_EXCLUDES + ["internal_*.h"],
    ),
    include_prefix = "drake_ros/tf2",
    visibility = ["//visibility:public"],
    deps = [
        "//core",
        "@drake//geometry:scene_graph_inspector",
        "@drake//multibody/plant",
        "@ros2//:geometry_msgs_cc",
        "@ros2//:rclcpp_cc",
        "@ros2//:tf2_ros_cc",
    ],
)

ros_cc_test(
    name = "test_tf_broadcaster",
    size = "small",
    srcs = ["test/test_tf_broadcaster.cc"],
    rmw_implementation = "rmw_cyclonedds_cpp",
    deps = [
        ":tf2",
        "@com_google_googletest//:gtest_main",
        "@drake//common",
        "@drake//systems/primitives",
        "@ros2//:geometry_msgs_cc",
        "@ros2//:rclcpp_cc",
        "@ros2//:tf2_ros_cc",
        "@ros2//resources/rmw_isolation:rmw_isolation_cc",
    ],
)

cc_library(
    name = "python_bindings_internal_hdrs",
    hdrs = glob(["*_pybind.h"]),
    include_prefix = "drake_ros/tf2",
)

pybind_py_library(
    name = "cc_py",
    cc_deps = [
        # TODO(sloretz) - depend on a shared library target
        # This links statically, and so can lead to ODR violations if the
        # python and C++ libraries are depended upon by the same python target.
        ":tf2",
        ":python_bindings_internal_hdrs",
    ],
    cc_so_name = "_cc",
    cc_srcs = ["cc_py.cc"],
    py_deps = [
        "//:module_py",
        "//core:core_py",
    ],
)

py_library(
    name = "module_py",
    srcs = ["__init__.py"],
    deps = [":cc_py"],
)

py_library(
    name = "tf2_py",
    visibility = ["//visibility:public"],
    deps = [":module_py"],
)

ros_py_test(
    name = "tf2_py_test",
    srcs = ["test/tf2_py_test.py"],
    main = "test/tf2_py_test.py",
    deps = [":tf2_py"],
)

ros_py_test(
    name = "test_tf_broadcaster_py",
    srcs = ["test/test_tf_broadcaster.py"],
    legacy_create_init = False,
    main = "test/test_tf_broadcaster.py",
    deps = [
        ":tf2_py",
        "@drake//bindings/pydrake",
        "@ros2//:rclpy_py",
        "@ros2//:tf2_ros_py_py",
        "@ros2//resources/rmw_isolation:rmw_isolation_py",
    ],
)
