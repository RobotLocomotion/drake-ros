load("@ros2//:ros_cc.bzl", "ros_cc_test")
load("@ros2//:ros_py.bzl", "ros_py_test")
load(
    "@drake//tools/skylark:pybind.bzl",
    "pybind_py_library",
)

PYBIND_EXCLUDES = [
    "**/*_py.cc",
    "**/*_pybind.h",
]

# TODO(sloretz) more granular targets for static linking
cc_library(
    name = "viz",
    srcs = glob(
        [
            "*.cc",
            "*.h",
        ],
        exclude = PYBIND_EXCLUDES,
    ),
    hdrs = glob(
        ["*.h"],
        exclude = PYBIND_EXCLUDES + ["internal_*.h"],
    ),
    include_prefix = "drake_ros/viz",
    visibility = ["//visibility:public"],
    deps = [
        "//core",
        "//tf2",
        "@drake//common",
        "@drake//geometry",
        "@drake//math",
        "@drake//multibody/plant",
        "@drake//systems/framework",
        "@ros2//:geometry_msgs_cc",
        "@ros2//:rclcpp_cc",
        "@ros2//:visualization_msgs_cc",
    ],
)

ros_cc_test(
    name = "test_name_conventions",
    size = "small",
    srcs = ["test/test_name_conventions.cc"],
    includes = ["."],
    rmw_implementation = "rmw_cyclonedds_cpp",
    deps = [
        ":viz",
        "@com_google_googletest//:gtest_main",
    ],
)

ros_cc_test(
    name = "test_scene_markers",
    size = "small",
    srcs = ["test/test_scene_markers.cc"],
    rmw_implementation = "rmw_cyclonedds_cpp",
    deps = [
        ":viz",
        "@com_google_googletest//:gtest_main",
        "@drake//common",
        "@drake//geometry",
        "@drake//math",
        "@drake//multibody/plant",
        "@drake//systems/framework",
        "@drake//systems/primitives",
        "@ros2//:visualization_msgs_cc",
    ],
)

cc_library(
    name = "python_bindings_internal_hdrs",
    hdrs = glob(["*_pybind.h"]),
    include_prefix = "drake_ros/viz",
)

pybind_py_library(
    name = "viz_py",
    cc_deps = [
        # TODO(sloretz) - depend on a shared library target
        # This links statically, and so can lead to ODR violations if the
        # python and C++ libraries are depended upon by the same python target.
        ":viz",
        ":python_bindings_internal_hdrs",
    ],
    cc_so_name = "drake_ros_viz",
    cc_srcs = glob(["*_py.cc"]),
    py_deps = [
        "//core:core_py",
        "//tf2:tf2_py",
    ],
    py_imports = ["."],
    visibility = ["//visibility:public"],
)

ros_py_test(
    name = "test_python_bindings",
    srcs = ["test/test_python_bindings.py"],
    legacy_create_init = False,
    main = "test/test_python_bindings.py",
    deps = [
        ":viz_py",
        "//core:core_py",
        "@drake//bindings/pydrake",
        "@ros2//:rclpy_py",
        "@ros2//:visualization_msgs_py",
        "@ros2//resources/rmw_isolation:rmw_isolation_py",
    ],
)
