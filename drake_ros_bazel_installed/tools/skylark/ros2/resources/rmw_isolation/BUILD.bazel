# -*- python -*-
# vi: set ft=python :

load("//:ros_cc.bzl", "ros_cc_binary")
load("//:ros_py.bzl", "ros_py_binary")

py_library(
    name = "rmw_isolation_py",
    srcs = ["rmw_isolation.py"],
    deps = ["//:rclpy_py"],
)

py_library(
    name = "module_py",
    srcs = ["__init__.py"],
    deps = [":rmw_isolation_py"],
)

ros_py_binary(
    name = "isolated_rmw_env",
    srcs = ["isolated_rmw_env.py"],
    main = "isolated_rmw_env.py",
    deps = [":module_py"],
    legacy_create_init = False,
)

genrule(
    name = "rmw_isolation_cc_gen",
    srcs = ["rmw_isolation.cc.in"],
    outs = ["rmw_isolation.cc"],
    cmd = (
        "ISOLATED_RMW_ENV_PATH={}/rmw_isolation/isolated_rmw_env ".format(
            repository_name().strip("@") or "."
        ) + "envsubst < $< > $@"
    ),
)

cc_library(
    name = "rmw_isolation_cc",
    srcs = ["rmw_isolation.cc"],
    hdrs = ["rmw_isolation.h"],
    data = [":isolated_rmw_env"],
    deps = [
        "@bazel_tools//tools/cpp/runfiles",
        "//:rclcpp_cc",
    ],
)

ros_py_binary(
    name = "talker_py",
    srcs = ["test/talker.py"],
    main = "test/talker.py",
    deps = [
        "//:rclpy_py",
        "//:std_msgs_py"
    ],
    rmw_implementation = 'rmw_cyclonedds_cpp',
    legacy_create_init = False,
)

ros_py_binary(
    name = "isolated_listener_py",
    srcs = ["test/isolated_listener.py"],
    main = "test/isolated_listener.py",
    deps = [
        "//:rclpy_py",
        "//:std_msgs_py",
        ":module_py",
    ],
    rmw_implementation = 'rmw_cyclonedds_cpp',
    legacy_create_init = False,
)

sh_test(
    name = "rmw_isolation_py_test",
    srcs = ["test/rmw_isolation_test.sh"],
    args = [
        "$(location :talker_py)",
        "$(location :isolated_listener_py)",
    ],
    data = [":talker_py", ":isolated_listener_py"],
    size = "small",
)

ros_cc_binary(
    name = "isolated_listener_cc",
    srcs = ["test/isolated_listener.cc"],
    deps = [
        "//:rclcpp_cc",
        "//:std_msgs_cc",
        ":rmw_isolation_cc",
    ],
    rmw_implementation = 'rmw_cyclonedds_cpp',
)

sh_test(
    name = "rmw_isolation_cc_test",
    srcs = ["test/rmw_isolation_test.sh"],
    args = [
        "$(location :talker_py)",
        "$(location :isolated_listener_cc)",
    ],
    data = [":talker_py", ":isolated_listener_cc"],
    size = "small",
)
