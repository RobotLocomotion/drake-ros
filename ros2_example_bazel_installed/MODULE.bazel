module(name = "ros2_example_bazel_installed")

bazel_dep(name = "bazel_skylib", version = "1.8.2")
bazel_dep(name = "rules_python", version = "1.7.0")

DRAKE_COMMIT = "v1.48.0"

DRAKE_CHECKSUM = (
    "f03fcfebf66902a4f160605215a72d1889d33efbadc5fb008535f42ad8fd6690"
)

bazel_dep(name = "drake")
archive_override(
    module_name = "drake",
    sha256 = DRAKE_CHECKSUM,
    strip_prefix = "drake-{}".format(DRAKE_COMMIT.lstrip("v")),
    urls = [x.format(DRAKE_COMMIT) for x in [
        "https://github.com/RobotLocomotion/drake/archive/{}.tar.gz",
    ]],
    patches = [
        "tools/workspace/drake/patches/recover_dev_dependencies.patch",
        "tools/workspace/drake/patches/use_label_in_python_lint.patch",
        "tools/workspace/drake/patches/use_label_in_cpplint.patch",
        "tools/workspace/drake/patches/use_drake_clang_format.patch",
        "tools/workspace/drake/patches/fix_gtest_printers_visibility.patch",
    ],
)

drake_dep_repositories = use_extension(
    "@drake//tools/workspace:default.bzl",
    "drake_dep_repositories",
)
use_repo(drake_dep_repositories, "pycodestyle", "styleguide")

bazel_dep(name = "bazel_ros2_rules")
local_path_override(
    module_name = "bazel_ros2_rules",
    path = "../bazel_ros2_rules",
)

# Please keep this list sorted
ROS_REQUIRED_PACKAGES = [
    "console_bridge_vendor",
    "rclcpp",
    "rclcpp_action",
    "rclpy",
    "rosbag2",
    "ros2bag_mcap_cli",
    "ros2bag_sqlite3_cli",
    "tf2_py",
] + [
    # These are possible RMW implementations. Uncomment one and only one to
    # change implementations
    "rmw_cyclonedds_cpp",
    # "rmw_fastrtps_cpp",
]

local_ros2 = use_extension(
    "@bazel_ros2_rules//lib:extensions.bzl",
    "local_ros2",
)
local_ros2.distribution(
    include_packages = ROS_REQUIRED_PACKAGES,
)
use_repo(local_ros2, ros2 = "local_ros2")

local_runtime_repo = use_repo_rule(
    "@rules_python//python/local_toolchains:repos.bzl",
    "local_runtime_repo",
)

local_runtime_toolchains_repo = use_repo_rule(
    "@rules_python//python/local_toolchains:repos.bzl",
    "local_runtime_toolchains_repo",
)

local_runtime_repo(
    name = "local_python3",
    interpreter_path = "python3",
    on_failure = "fail",
)

local_runtime_toolchains_repo(
    name = "local_toolchains",
    runtimes = ["local_python3"],
)

register_toolchains("@local_toolchains//:all")
