workspace(name = "ros2_example_bazel_installed")

load("//tools:environ.bzl", "environment_repository")
environment_repository(
    name = "ros2_example_bazel_installed_environ",
    envvars = ["ROS2_DISTRO_PREFIX"],
)
load("@ros2_example_bazel_installed_environ//:environ.bzl", "ROS2_DISTRO_PREFIX")

local_repository(
    name = "bazel_ros2_rules",
    path = "../bazel_ros2_rules",
)
load("@bazel_ros2_rules//deps:defs.bzl", "add_bazel_ros2_rules_dependencies")
add_bazel_ros2_rules_dependencies()

load("@bazel_ros2_rules//ros2:defs.bzl", "ros2_archive")
load("@bazel_ros2_rules//ros2:defs.bzl", "ros2_local_repository")

ROS2_PACKAGES = [
    "action_msgs",
    "builtin_interfaces",
    "rosidl_default_generators",
    "rclcpp_action",
    "rclcpp",
    "rclpy",
    # RMW implementations
    "rmw_cyclonedds_cpp",
    "rmw_fastrtps_cpp",
]

ros2_archive(
    name = "ros2" if not ROS2_DISTRO_PREFIX else "ros2_ignored",
    include_packages = ROS2_PACKAGES,
    # Note: If you want an exact / unchanging version, you will need to mirror the archive
    # according to versions you want.
    # TODO(hidmic,cottsay): Make release-pinned snapshots of this repository once `drake-ros`
    # itself has versions.
    url = "http://repo.ros2.org/ci_archives/drake-ros-underlay/ros2-rolling-linux-focal-amd64-ci.tar.bz2",
    sha256_url = "https://repo.ros2.org/ci_archives/drake-ros-underlay/ros2-rolling-linux-focal-amd64-ci-CHECKSUM",
    strip_prefix = "ros2-linux",
)

ros2_local_repository(
    name = "ros2" if ROS2_DISTRO_PREFIX else "ros2_ignored",
    workspaces = [ROS2_DISTRO_PREFIX],
    include_packages = ROS2_PACKAGES,
)
